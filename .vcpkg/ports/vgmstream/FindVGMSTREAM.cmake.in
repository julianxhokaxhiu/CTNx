# Distributed under the OSI-approved BSD 3-Clause License.
#
#.rst:
# FindVGMSTREAM
# --------
#
# Find the VGMSTREAM libraries
#
# Result Variables
# ^^^^^^^^^^^^^^^^
#
# The following variables will be defined:
#
#  ``VGMSTREAM_FOUND``
#    True if VGMSTREAM found on the local system
#
#  ``VGMSTREAM_INCLUDE_DIRS``
#    Location of VGMSTREAM header files
#
#  ``VGMSTREAM_LIBRARY_DIRS``
#    Location of VGMSTREAM libraries
#
#  ``VGMSTREAM_LIBRARIES``
#    List of the VGMSTREAM libraries found
#
#

include(FindPackageHandleStandardArgs)
include(SelectLibraryConfigurations)
include(CMakeFindDependencyMacro)

if(NOT VGMSTREAM_FOUND)

# Compute the installation path relative to this file.
get_filename_component(SEARCH_PATH "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(SEARCH_PATH "${SEARCH_PATH}" PATH)
get_filename_component(SEARCH_PATH "${SEARCH_PATH}" PATH)
if(SEARCH_PATH STREQUAL "/")
  set(SEARCH_PATH "")
endif()

set(VGMSTREAM_VERSION "@VGMSTREAM_VERSION@")

function(append_dependencies out)
    cmake_parse_arguments(PARSE_ARGV 1 "arg" "DEBUG" "NAMES" "")
    if(${arg_DEBUG})
        set(config DEBUG)
        set(path "${CURRENT_INSTALLED_DIR}/debug/lib/")
    else()
        set(config RELEASE)
        set(path "${CURRENT_INSTALLED_DIR}/lib/")
    endif()
    foreach(lib_name ${arg_NAMES})
        if("${lib_name}" STREQUAL "-pthread")
            list(APPEND ${out} "-pthread")
        elseif("${lib_name}" STREQUAL "-pthreads")
            list(APPEND ${out} "-pthreads")
        elseif("${lib_name}" STREQUAL "gcc")
            list(APPEND ${out} "-lgcc")
        elseif("${lib_name}" STREQUAL "gcc_s")
            list(APPEND ${out} "-lgcc_s")
        elseif("${lib_name}" STREQUAL "stdc++")
            list(APPEND ${out} "-lstdc++")
        else()
            # first look in ${path} specifically to ensure we find the right release/debug variant
            find_library(VGMSTREAM_DEPENDENCY_${lib_name}_${config} NAMES "${lib_name}" PATHS "${path}" NO_DEFAULT_PATH)
            # if not found there, must be a system dependency, so look elsewhere
            find_library(VGMSTREAM_DEPENDENCY_${lib_name}_${config} NAMES "${lib_name}" REQUIRED)
            list(APPEND ${out} "${VGMSTREAM_DEPENDENCY_${lib_name}_${config}}")
        endif()
    endforeach()
    set("${out}" "${${out}}" PARENT_SCOPE)
endfunction()

macro(VGMSTREAM_FIND libname shortname headername)
  if(NOT VGMSTREAM_${libname}_INCLUDE_DIRS)
    find_path(VGMSTREAM_${libname}_INCLUDE_DIRS NAMES ${headername} PATHS ${SEARCH_PATH}/include NO_DEFAULT_PATH)
  endif()
  if(NOT VGMSTREAM_${libname}_LIBRARY)
    find_library(VGMSTREAM_${libname}_LIBRARY_RELEASE NAMES ${shortname} PATHS ${SEARCH_PATH}/lib/ NO_DEFAULT_PATH)
    find_library(VGMSTREAM_${libname}_LIBRARY_DEBUG NAMES ${shortname} PATHS ${SEARCH_PATH}/debug/lib/ NO_DEFAULT_PATH)
    get_filename_component(VGMSTREAM_${libname}_LIBRARY_RELEASE_DIR ${VGMSTREAM_${libname}_LIBRARY_RELEASE} DIRECTORY)
    get_filename_component(VGMSTREAM_${libname}_LIBRARY_DEBUG_DIR ${VGMSTREAM_${libname}_LIBRARY_DEBUG} DIRECTORY)
    select_library_configurations(VGMSTREAM_${libname})
    set(VGMSTREAM_${libname}_LIBRARY ${VGMSTREAM_${libname}_LIBRARY} CACHE STRING "")
  endif()
  if (VGMSTREAM_${libname}_LIBRARY AND VGMSTREAM_${libname}_INCLUDE_DIRS)
    set(VGMSTREAM_${libname}_FOUND TRUE BOOL)
    list(APPEND VGMSTREAM_INCLUDE_DIRS ${VGMSTREAM_${libname}_INCLUDE_DIRS})
    list(APPEND VGMSTREAM_LIBRARIES ${VGMSTREAM_${libname}_LIBRARY})
    list(APPEND VGMSTREAM_LIBRARY_DIRS ${VGMSTREAM_${libname}_LIBRARY_RELEASE_DIR} ${VGMSTREAM_${libname}_LIBRARY_DEBUG_DIR})
  endif()
endmacro(VGMSTREAM_FIND)

VGMSTREAM_FIND(vgmstream libvgmstream libvgmstream/vgmstream.h)

if (VGMSTREAM_vgmstream_FOUND)
  list(APPEND VGMSTREAM_INCLUDE_DIRS "${SEARCH_PATH}/include/libvgmstream")
  list(REMOVE_DUPLICATES VGMSTREAM_INCLUDE_DIRS)
  list(REMOVE_DUPLICATES VGMSTREAM_LIBRARY_DIRS)
  set(VGMSTREAM_vgmstream_VERSION "@VGMSTREAM_VERSION@" CACHE STRING "")

  append_dependencies(VGMSTREAM_DEPS_LIBRARY_RELEASE NAMES "@VGMSTREAM_DEPENDENCIES_RELEASE@")
  append_dependencies(VGMSTREAM_DEPS_LIBRARY_DEBUG   NAMES "@VGMSTREAM_DEPENDENCIES_DEBUG@" DEBUG)
  if(VGMSTREAM_DEPS_LIBRARY_RELEASE OR VGMSTREAM_DEPS_LIBRARY_DEBUG)
    select_library_configurations(VGMSTREAM_DEPS)
    list(APPEND VGMSTREAM_LIBRARIES ${VGMSTREAM_DEPS_LIBRARY})
  endif()

  set(VGMSTREAM_LIBRARY ${VGMSTREAM_LIBRARIES})

  set(VGMSTREAM_FOUND TRUE CACHE BOOL "")
  set(VGMSTREAM_LIBRARIES ${VGMSTREAM_LIBRARIES} CACHE STRING "")
  set(VGMSTREAM_INCLUDE_DIRS ${VGMSTREAM_INCLUDE_DIRS} CACHE STRING "")
  set(VGMSTREAM_LIBRARY_DIRS ${VGMSTREAM_LIBRARY_DIRS} CACHE STRING "")
endif()

find_package_handle_standard_args(VGMSTREAM REQUIRED_VARS VGMSTREAM_LIBRARIES VGMSTREAM_LIBRARY_DIRS VGMSTREAM_INCLUDE_DIRS)

endif()
